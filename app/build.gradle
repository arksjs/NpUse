plugins {
    id 'com.android.application'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.lyentech.npuse"
        minSdk 21
        targetSdk 33
        versionCode 5
        versionName "1.2"

        flavorDimensions "gree", "np"
    }

    //多渠道打包
    applicationVariants.all { variant ->   //配置默认包名写法
        variant.outputs.all { output ->
            def fileName = "v${variant.versionName}.${defaultConfig.versionCode}_np_${variant.productFlavors[0].name}_${variant.productFlavors[1].name}_${buildType.name}.apk"
            def outFile = output.outputFile
            if (outFile != null && outFile.name.endsWith('.apk')) {
                outputFileName = new File(outFile.parent, fileName).getName()
            }
        }
    }

    productFlavors {
        gree {
            dimension "gree"
            buildConfigField "String", "FLAVOR_NAME", "\"gree\""
        }
        npdev {
            dimension "np"
            buildConfigField "String", "FLAVOR_NAME", "\"npdev\""
            manifestPlaceholders = [
                    app_name: "dev_np"
            ]
        }
        npque {
            dimension "np"
            buildConfigField "String", "FLAVOR_NAME", "\"npque\""
            manifestPlaceholders = [
                    app_name: "que_np"
            ]
        }
        nppro {
            dimension "np"
            buildConfigField "String", "FLAVOR_NAME", "\"nppro\""
            manifestPlaceholders = [
                    app_name: "pro_np"
            ]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.30'

    //本地依赖代码使用
//    implementation project(':use')

    //远程依赖使用
    implementation 'com.github.arksjs:NpUse-Android:v1.2.2'  //改仓库名后也能用
}